module Camera;

import Vec3;
import Ray;
import MathUtil;

type Camera = struct {
    origin : Point3,
    horizontal : Vec3,
    vertical : Vec3,
    lower_left_corner : Vec3,
};

namespace Camera {
    make_camera : Point3 -> Point3 -> Vec3 -> F64 -> F64 -> Camera;
    make_camera = |lookfrom, lookat, vup, vfov, aspect_ratio| (
        let theta = vfov.degrees_to_radians;
        let h = tan(theta / 2.0);
        let viewport_height : F64 = 2.0 * h;
        let viewport_width : F64 = aspect_ratio * viewport_height;

        let w = (lookfrom - lookat).unit;
        let u = cross(vup, w).unit;
        let v = cross(w, u);

        let origin : Vec3 = lookfrom;
        let horizontal = viewport_width.times(u);
        let vertical = viewport_height.times(v);
        let lower_left_corner = origin - horizontal.div_by(2.0) - vertical.div_by(2.0) - w;

        Camera {
            origin : origin,
            horizontal : horizontal,
            vertical : vertical,
            lower_left_corner : lower_left_corner,
        }
    );

    get_ray : F64 -> F64 -> Camera -> Ray;
    get_ray = |u, v, camera| (
        Ray {
            origin : camera.@origin,
            direction : camera.@lower_left_corner + u.times(camera.@horizontal) + v.times(camera.@vertical) - camera.@origin
        }
    );
}